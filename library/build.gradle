apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'checkstyle'

android {
    compileSdkVersion android_compile_sdk_version
    buildToolsVersion android_build_tools_version

    defaultConfig {
        minSdkVersion android_min_sdk_version
        targetSdkVersion android_target_sdk_version
        versionCode 1
        versionName "1.0"
    }
}

dependencies {
    api "androidx.viewpager:viewpager:1.0.0"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

checkstyle {
    configFile = new File(rootDir, 'checkstyle.xml')
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/lisb/ViewPagerIndicator"
                credentials {
                    username = gpr_user
                    password = gpr_password
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.lisb.android.viewpagerindicator'
                artifactId = 'viewpagerindicator3'
                version = '3.0.0-SNAPSHOT'
            }
        }
    }
}
repositories {
    mavenCentral()
}
