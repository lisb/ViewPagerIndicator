apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'checkstyle'

android {
    compileSdkVersion android_compile_sdk_version
    buildToolsVersion android_build_tools_version

    defaultConfig {
        minSdkVersion android_min_sdk_version
        targetSdkVersion android_target_sdk_version
        versionCode 1
        versionName "1.0"
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

checkstyle {
    configFile = new File(rootDir, 'checkstyle.xml')
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.source
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = publish_repository_url
                credentials {
                    username = gpr_user
                    password = gpr_password
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                artifact tasks.androidSourcesJar
                groupId = publish_groupId
                artifactId = 'viewpagerindicator3'
                version = publish_version
            }
        }
    }
}